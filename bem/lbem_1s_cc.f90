!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Shimon Panfil: Industrial Physics and Simulations                   !!
! http://industrialphys.com                                           !!
! THE SOFTWARE IS PROVIDED "AS IS", USE IT AT YOUR OWN RISK           !!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
MODULE LBEM_1S_CC
! ONE SURFACE, CONSTANT CHARGES ATTRIBUTED TO CENTERS OF TRIANGS    
USE TSURF
IMPLICIT NONE
PUBLIC

    TYPE(TSURF_TYPE)::SRF ! TRIANGULATED SURFACE
    INTEGER::NU ! NUMBER OF UNKNOWNS AND CONTROL POINTS
    ! Large arrays 
    REAL(F64),ALLOCATABLE,DIMENSION(:,:)::LM !(NU,NU)
    REAL(F64),ALLOCATABLE,DIMENSION(:)::QTOT !(NU)
    REAL(F64),ALLOCATABLE,DIMENSION(:)::Q !(NU) AUXILIARY
    REAL(F64),ALLOCATABLE,DIMENSION(:)::VV !(NU) USED IN LUDCMP
    REAL(F64),ALLOCATABLE,DIMENSION(:)::CHRG !(NU) CHARGES
    REAL(F32),ALLOCATABLE,DIMENSION(:,:)::CP !(3,NU)
    REAL(F64),ALLOCATABLE,DIMENSION(:)::XPOT !(NU)
    REAL(F64),ALLOCATABLE,DIMENSION(:)::XPOT1 !(NU)
    INTEGER,ALLOCATABLE,DIMENSION(:)::INDX !(NU) USED IN LUDCMP,LUBKSP


    REAL(F64)::PHI_NRM
 
CONTAINS
! CALLED WHEN SRF INITIALIZED
SUBROUTINE INIT_LBEM(FLAG)
    INTEGER::FLAG,I
    NU=SRF%NT
    ALLOCATE(LM(NU,NU),QTOT(NU),XPOT(NU),XPOT1(NU),CP(3,NU),Q(NU),CHRG(NU),VV(NU),INDX(NU),STAT=FLAG)
    IF(FLAG/=0) RETURN
    CALL LBEM_MK_QTOT1(SRF)
    CALL LBEM_MK_CNTR1(SRF)
    CALL LBEM_MK_MAT1(SRF)
    CALL LUDCMP1(LM,NU,INDX,VV,FLAG)
    IF(FLAG/=0) RETURN
    Q=ONE_F64
    CALL LUBKSB1(LM,NU,INDX,Q)
    PHI_NRM=ZERO_F64
    DO I=1,NU
    PHI_NRM=PHI_NRM+QTOT(I)*Q(I)
    ENDDO
ENDSUBROUTINE INIT_LBEM

SUBROUTINE CLEAN_LBEM()
    CALL CLEAN_TSURF(SRF)
    IF(ALLOCATED(LM)) DEALLOCATE(LM)
    IF(ALLOCATED(QTOT)) DEALLOCATE(QTOT)
    IF(ALLOCATED(XPOT)) DEALLOCATE(XPOT)
    IF(ALLOCATED(XPOT1)) DEALLOCATE(XPOT1)
    IF(ALLOCATED(CP)) DEALLOCATE(CP)
    IF(ALLOCATED(Q)) DEALLOCATE(Q)
    IF(ALLOCATED(CHRG)) DEALLOCATE(CHRG)
    IF(ALLOCATED(VV)) DEALLOCATE(VV)
    IF(ALLOCATED(INDX)) DEALLOCATE(INDX)
ENDSUBROUTINE CLEAN_LBEM

! CONSTANT CHARGES
SUBROUTINE LBEM_MK_QTOT1(S)
    TYPE(TSURF_TYPE):: S
    INTEGER::I,V1,V2,V3
    REAL(F32)::A,B(3),C(3),W(3)
    DO I=1,S%NT 
        V1=S%TVEC(1,I)
        V2=S%TVEC(2,I)
        V3=S%TVEC(3,I)
        B=S%VVEC(:,V2)-S%VVEC(:,V1)
        C=S%VVEC(:,V3)-S%VVEC(:,V1)
        W=CROSS_F32(B,C)
        A=HALF_F32*SQRT(DOT_F32(W,W))
        QTOT(I)=A
    ENDDO
ENDSUBROUTINE LBEM_MK_QTOT1

SUBROUTINE LBEM_MK_CNTR1(S)
    TYPE(TSURF_TYPE):: S
    INTEGER::I,V1,V2,V3
    DO I=1,S%NT 
        V1=S%TVEC(1,I)
        V2=S%TVEC(2,I)
        V3=S%TVEC(3,I)
        CP(:,I)=THIRD_F32*(S%VVEC(:,V1)+S%VVEC(:,V2)+S%VVEC(:,V3))
    ENDDO
ENDSUBROUTINE LBEM_MK_CNTR1

SUBROUTINE LBEM_MK_MAT1(S)
    TYPE(TSURF_TYPE):: S
    REAL(F64)::LPL_GF_TSURF1
    EXTERNAL  LPL_GF_TSURF1
    INTEGER::I,J
    DO I=1,NU
        DO J=1,NU 
            LM(J,I)=LPL_GF_TSURF1(S,CP(:,J),I)
         ENDDO
    ENDDO
ENDSUBROUTINE LBEM_MK_MAT1


SUBROUTINE GET_XPOT(PNT)
    REAL(F32)::PNT(3)
    INTEGER::I
    REAL(F64)::PSI
    EXTERNAL PSI
    DO I=1,NU
        XPOT(I)=PSI(PNT+CP(:,I))
    ENDDO
ENDSUBROUTINE GET_XPOT

FUNCTION GET_ENERGY1(SHIFT) RESULT(RES)
    REAL(F32)::SHIFT(3)
    REAL(F64)::RES
    INTEGER::J
    REAL(F64)::PHI
    CALL GET_XPOT(SHIFT)
    XPOT1=XPOT
    CALL LUBKSB1(LM,NU,INDX,XPOT1)
    PHI=ZERO_F64
    DO J=1,NU
        PHI=PHI+QTOT(J)*XPOT1(J)
    ENDDO
    PHI=PHI/PHI_NRM !POTENTIAL ON SURFACE
    DO J=1,NU
        CHRG(J)=PHI*Q(J)-XPOT1(J)
    ENDDO
    RES=ZERO_F64
    DO J=1,NU
        RES=RES+CHRG(J)*XPOT(J)
    ENDDO
ENDFUNCTION GET_ENERGY1

ENDMODULE LBEM_1S_CC
